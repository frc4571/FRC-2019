import edu.wpi.first.gradlerio.GradleRIOPlugin

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "edu.wpi.first:GradleRIO:$wpilib_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'edu.wpi.first.GradleRIO'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = java_version
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = java_version
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}

subprojects {
    buildDir = file("$rootDir/build/${project.name}")
}

deploy {
    targets {
        roboRIO("roborio") {
            team = TEAM.toInteger()
        }
    }
    artifacts {
        frcJavaArtifact('frcJava') {
            targets << "roborio"
            debug = frc.getDebugOrDefault(false)
        }
        fileTreeArtifact('frcStaticFileDeploy') {
            files = fileTree(dir: 'src/main/deploy')
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
    compile project(':rampage')
    compile project(':rampage:ctre-motion')
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest GradleRIOPlugin.javaManifest(MAIN_CLASS)
}

wrapper {
    gradleVersion gradle_version
}